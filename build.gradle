plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
}

ext.kotlin_version = '1.3.71'

group = pluginGroup
version = pluginVersion

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }

    maven {
        name = "github-towny"
        url = uri("https://maven.pkg.github.com/TownyAdvanced/Towny")
        credentials {
            username = properties.getProperty("github.user") ?: System.getenv("GITHUB_USERNAME")
            password = properties.getProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }

    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT'
    compileOnly "com.palmergames.bukkit.towny:Towny:0.96.0.0"
    compileOnly name: 'PopulationDensity'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "$targetCompatibility"
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

